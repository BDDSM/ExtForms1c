
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД

&НаСервере
Процедура СформироватьОтчетНаСервере()
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	
	ТабДок = ОтчетОбъект.СформироватьМакетТаблицыОтчета(Отчет.ПериодОтчета);
	
	ПолеОтчета.Очистить();
	ПолеОтчета.Вывести(ТабДок);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПриИзмененииВидаОтчета()
	
	Если Отчет.ВидОтчета = "Основной" Тогда
		
		// настройки
		Элементы.ВыводитьПустыеЗначения.Видимость = Истина;
		Элементы.НеВыводитьИнформациюОТабличныхЧастях.Видимость = Истина;
		
		// период
		Элементы.ГруппаПериод.Видимость = Ложь;
		
		// структура
		Элементы.ГруппаСтруктура.Видимость = Ложь;
		
		// сумма
		
	ИначеЕсли Отчет.ВидОтчета = "ПоПериодам" Тогда
		
		// настройки
		Элементы.ВыводитьПустыеЗначения.Видимость = Истина;
		Элементы.НеВыводитьИнформациюОТабличныхЧастях.Видимость = Ложь;
		
		// период
		Элементы.ГруппаПериод.Видимость = Истина;
		
		// структура
		Элементы.ГруппаСтруктура.Видимость = Ложь;
		
		// сумма
		
	ИначеЕсли Отчет.ВидОтчета = "СтруктураБД" Тогда
		
		КлиентСервер = Отчет.ЭтоКлиентСервер;
		
		// настройки
		Элементы.ВыводитьПустыеЗначения.Видимость = Ложь;
		Элементы.НеВыводитьИнформациюОТабличныхЧастях.Видимость = Ложь;
		
		// период
		Элементы.ГруппаПериод.Видимость = Ложь;
		
		// структура
		Элементы.ГруппаСтруктура.Видимость = Истина;
		Элементы.РассчитыватьОбъемТаблицБД.Видимость = КлиентСервер;
		
		// сумма
		
	КонецЕсли;
	
	РассчитыватьОбъемТаблицБДПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура РассчитыватьОбъемТаблицБДПриИзмененииНаСервере()
	
	Если Отчет.ВидОтчета = "СтруктураБД" И НЕ Отчет.РассчитыватьОбъемТаблицБД Тогда
		
		Элементы.ГруппаСумма.Видимость = Ложь;
		Элементы.ПолеСотрировки.Видимость = Ложь;
		Элементы.ПолеСотрировки1.Видимость = Истина;
		Элементы.ГруппаБД.Видимость = Ложь;
		
		Если Отчет.ПолеСортировки <> "Имя" И Отчет.ПолеСортировки <> "Синоним" Тогда
			Отчет.ПолеСортировки = "Синоним";
		КонецЕсли;
		
	Иначе
		
		Элементы.ГруппаСумма.Видимость = Истина;
		Элементы.ПолеСотрировки.Видимость = Истина;
		Элементы.ПолеСотрировки1.Видимость = Ложь;
		Элементы.ГруппаБД.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	
	Отчет.ПолеСортировки = "КоличествоУбыв";
	Отчет.Периодичность = "МЕСЯЦ";
	
	Отчет.ВидОтчета = "Основной";
	ПриИзмененииВидаОтчета();
	
	Отчет.ЭтоКлиентСервер = ОтчетОбъект.ЭтоКлиентСервернаяБаза();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОтчетаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФорму(Расшифровка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОтчетаПриАктивизацииОбласти(Элемент)
	
	Сумма = 0;
	
	Для Каждого ВыделеннаяОбласть Из ПолеОтчета.ВыделенныеОбласти Цикл
		
		Для Стр = ВыделеннаяОбласть.Верх По ВыделеннаяОбласть.Низ Цикл
			Для Кол = ВыделеннаяОбласть.Лево По ВыделеннаяОбласть.Право Цикл
				
				Попытка
					Ячейка = ПолеОтчета.Область(Стр, Кол, Стр, Кол);
					Если Ячейка.СодержитЗначение Тогда
						Сумма = Сумма + Ячейка.Значение;
					КонецЕсли;
				Исключение
				КонецПопытки;
				
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
	ВыделеннаяСумма = Сумма;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОтчетаПриИзменении(Элемент)
	
	ПриИзмененииВидаОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитыватьОбъемТаблицБДПриИзменении(Элемент)
	
	РассчитыватьОбъемТаблицБДПриИзмененииНаСервере();
	
КонецПроцедуры
